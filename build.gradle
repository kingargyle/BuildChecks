buildscript {
    ext.kotlin_version = '1.2.30'
    ext.okHttpVersion = "3.9.0"
    ext.retrofitVersion = '2.4.0'
    repositories {
        jcenter()
        flatDir { dirs "build/libs" }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.10"
        classpath "com.toddway:buildChecks:1.8"

        //these are here because they're dependencies of the buildChecks.jar file
        classpath 'io.reactivex.rxjava2:rxjava:2.1.14'
        classpath "com.squareup.retrofit2:retrofit:$retrofitVersion"
        classpath "com.squareup.retrofit2:converter-gson:$retrofitVersion"
        classpath "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
        classpath "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.0.0.RC8"
}

apply plugin: "com.gradle.plugin-publish"
apply plugin: 'kotlin'

repositories {
    jcenter()
}

dependencies {
    implementation gradleApi()
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"

    implementation 'io.reactivex.rxjava2:rxjava:2.1.14'
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"

    testImplementation 'junit:junit:4.12'
    testImplementation 'com.nhaarman:mockito-kotlin:1.5.0'
    testImplementation 'org.amshove.kluent:kluent:1.34'
}

version = "1.8"
group = "com.toddway"
jar.baseName = "buildChecks"

pluginBundle {
    website = "https://github.com/toddway/BuildChecks"
    vcsUrl = "https://github.com/toddway/BuildChecks.git"
    description = "post summaries of code analyzer output to GitHub or BitBucket"
    tags = ["build checks"]

    plugins {
        buildChecks {
            id = "com.toddway.buildchecks"
            displayName = "Build Checks"
        }
    }
}

apply from: 'gradle/checks.gradle'

apply plugin: gradle.BuildChecksPlugin
buildChecks {
    baseUrl = System.properties["baseUrl"]
    authorization = System.properties["auth"]
    buildUrl = System.properties["buildUrl"]
    jacocoReports = "$projectDir/build/reports/jacoco/coverage/coverage.xml"
    checkstyleReports = "$projectDir/build/reports/detekt-checkstyle.xml"
    minCoveragePercent = 50
    maxLintViolations = 5
}
printChecks.dependsOn(build)
postChecks.dependsOn(build)
publishPlugins.dependsOn(postChecks)