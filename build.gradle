buildscript {
    ext.kotlin_version = '1.2.30'
    ext.okHttpVersion = "3.9.0"
    ext.retrofitVersion = '2.4.0'
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        flatDir {
            dirs "build/libs", "libs"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.10"
        classpath "com.toddway:buildStatusPlugin:1.1"

        //these are here because they're dependencies of the buildStatusPlugin jar file
        classpath 'io.reactivex.rxjava2:rxjava:2.1.14'
        classpath "com.squareup.retrofit2:retrofit:$retrofitVersion"
        classpath "com.squareup.retrofit2:converter-gson:$retrofitVersion"
        classpath "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
        classpath "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"
    }
}

apply plugin: BuildStatusPlugin
buildstatus {
    statusBaseUrl = "https://api.github.com/repos/toddway/buildStatusPlugin/"
    statusAuthorization = System.properties["githubAuth"]
    buildUrl = System.properties["buildUrl"]
    jacocoReports = "$projectDir/build/reports/jacoco/coverage/coverage.xml"
}

apply plugin: "com.gradle.plugin-publish"
apply plugin: 'kotlin'

repositories {
    jcenter()
}

dependencies {
    implementation gradleApi()
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"

    implementation 'io.reactivex.rxjava2:rxjava:2.1.14'
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"

    testImplementation 'junit:junit:4.12'
    testImplementation 'com.nhaarman:mockito-kotlin:1.5.0'
    testImplementation 'org.amshove.kluent:kluent:1.34'
}

test {
    //ignoreFailures true
    testLogging {
        exceptionFormat = 'full'
        showCauses true
        showExceptions true
        showStackTraces true
        showStandardStreams true
        events = ["passed", "skipped", "failed", "standardOut", "standardError"]
    }
}

version = "1.2"
group = "com.toddway"

pluginBundle {
    website = "https://github.com/toddway/buildStatusPlugin"
    vcsUrl = "https://github.com/toddway/buildStatusPlugin.git"
    description = "post summaries from code analyzers (jacoco, lint, detekt, and checkstyle) to VCS (GitHub & BitBucket)"
    tags = ["build status"]

    plugins {
        buildStatusPlugin {
            id = "com.toddway.buildstatusplugin"
            displayName = "Build Status plugin"
        }
    }
}

apply plugin: 'jacoco'
task coverage(type: JacocoReport, dependsOn: "test") {
    reports {
        xml.enabled = true
        html.enabled = true
    }

    // what to exclude from coverage report
    def excludes = [
            '**/R.class',
            '**/R$*.class',
            '**/*$ViewInjector*.*',
            '**/BuildConfig.*',
            '**/Manifest*.*',
            '**/*Test*.*',
            'android/**/*.*',
            '**/*Fragment.*',
            '**/*Activity.*',
            '**/*$Lambda$*.*', // Jacoco can not handle several "$" in class name.
            '**/*Module.*', // Modules for Dagger.
            '**/*Module_*Factory*', // Modules for Dagger.
            '**/*Dagger*.*', // Dagger auto-generated code.
            '**/*MembersInjector*.*', // Dagger auto-generated code.
            '**/*_Provide*Factory*.*',
            '**/*_Factory.*', //Dagger auto-generated code
            '**/*$*$*.*' // Anonymous classes generated by kotlin
    ]

    classDirectories = fileTree(
            dir: "$buildDir/classes/java", excludes: excludes
    ) + fileTree(
            dir: "$buildDir/classes/kotlin", excludes: excludes
    )

    sourceDirectories = files(["src/main/java"])
    executionData = files("$buildDir/jacoco/test.exec")
}

build.dependsOn(coverage)